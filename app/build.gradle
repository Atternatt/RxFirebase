apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'com.novoda.bintray-release'

android {

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

apply plugin: 'jacoco'

jacoco {
    toolVersion '0.7.8'
}

task codeCoverageReport(type: JacocoReport) {
    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled true
        csv.enabled false
    }

    classDirectories = fileTree(
            dir: 'build/classes/main/rxfirebase',
    )

    sourceDirectories = files('src/main/kotlin')
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    //firebase
    implementation "com.google.firebase:firebase-database:$firebaseVersion"

    //rx
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxJavaVersion"

    //test
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    testImplementation "org.assertj:assertj-core:$asseertjVersion"
    testImplementation "io.kotlintest:kotlintest:$kotlinTestVersion"
    testImplementation "com.google.firebase:firebase-database:$firebaseVersion"

}

publish {
    groupId = 'com.m2f'
    userOrg = POM_DEVELOP_ID
    artifactId = POM_ARTIFACT_ID
    publishVersion = '0.0.1'
    desc = 'RxJava2 Firebase Integartion Made in Kotlin'
    website = 'https://github.com/Atternatt/RxFirebase'
}